buildscript {
    ext {
        springBootVersion = '2.6.11'
        springCloudAlibabaVersion = '2021.0.4.0'
        springCloudVersion = '2021.0.4'
        //阿里云仓库地址
        registryUrl = 'registry.cn-hangzhou.aliyuncs.com'
        //阿里云仓库名
        registryNamespace = 'matrix-cloud'
        //阿里云仓库用户名及密码
        registryUsername = ''
        registryPassword = ''
    }
}

plugins {
    id "org.springframework.boot" version "${springBootVersion}" apply false
    id "io.spring.dependency-management" version '1.0.11.RELEASE' apply false
    id "com.google.cloud.tools.jib" version "3.3.0" apply false
}


wrapper.setGradleVersion("7.4.2")

//定义需要构建docker的模块
def javaMicroservices = [project(':matrix-gateway'),
                         project(':matrix-system:system-biz'),
                         project(':matrix-doc'),
                         project(':matrix-resource:resource-biz')]


allprojects {
    group = 'com.matrix'
    version = '0.0.1'
    description = "spring cloud matrix project for"

    repositories {
        //本地仓库
        mavenLocal()
        //阿里云
        maven {
            url 'https://maven.aliyun.com/nexus/content/groups/public/'
        }
        //中央仓库
        mavenCentral()
        //gradle插件
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}


subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencyManagement {
        imports {
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }

        dependencies {

            dependency 'io.seata:seata-spring-boot-starter:1.5.2'
            dependency 'com.baomidou:mybatis-plus-boot-starter:3.5.2'
            dependency 'cn.easy-es:easy-es-boot-starter:1.0.3'

            dependency "cn.dev33:sa-token-spring-boot-starter:1.31.0"
            dependency 'com.github.xiaoymin:knife4j-micro-spring-boot-starter:3.0.3'
            dependency 'com.alibaba:druid-spring-boot-starter:1.2.11'
            dependency 'com.baomidou:lock4j-redisson-spring-boot-starter:2.2.2'
            dependency "com.github.xiaoymin:knife4j-aggregation-spring-boot-starter:2.0.9"
            dependency "cn.dev33:sa-token-reactor-spring-boot-starter:1.30.0"


            /*other*/
            dependency 'mysql:mysql-connector-java:8.0.30'
            dependency "cn.dev33:sa-token-dao-redis-jackson:1.31.0"
            dependency "cn.dev33:sa-token-jwt:1.31.0"
            dependency "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.4"
            dependency 'io.swagger:swagger-annotations:1.5.22'
            dependency 'com.xuxueli:xxl-job-core:2.3.1'


            /*apm 监控相关*/
            dependency 'org.apache.skywalking:apm-toolkit-logback-1.x:8.12.0'
            dependency 'org.apache.skywalking:apm-toolkit-trace:8.12.0'
            dependency 'org.apache.skywalking:apm-toolkit-opentracing:8.12.0'
            dependency 'net.logstash.logback:logstash-logback-encoder:7.2'
            dependency 'io.micrometer:micrometer-registry-prometheus:1.9.5'

            /*utils*/
            dependency 'one.util:streamex:0.8.1'
            dependency "cn.hutool:hutool-all:5.8.3"
            dependency "com.google.guava:guava:31.1-jre"
            dependency "com.google.code.gson:gson:2.9.0"
            dependency "com.alibaba.fastjson2:fastjson2:2.0.8"
            dependency 'org.apache.commons:commons-pool2:2.11.1'
            dependency 'com.github.ben-manes.caffeine:caffeine:3.1.1'
            dependency 'com.alibaba:transmittable-thread-local:2.13.2'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
        compileOnly 'org.mapstruct:mapstruct:1.5.2.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    }
    test {
        useJUnitPlatform()
    }
}

File agentFile = file '/deploy/skywalking/agent'


configure(javaMicroservices) {
    apply plugin: 'com.google.cloud.tools.jib'

    // 使用jib插件自动构建镜像并上传到阿里云
    jib {
        from {
            image = "adoptopenjdk/openjdk11:ubuntu-jre-nightly"
        }
        //推送的目标镜像仓库地址
        to {
            image = "${registryUrl}/${registryNamespace}/${project.name}"
            //镜像tag
            tags = ["latest", "${project.version}".toString()]
            //身份认证信息
            auth {
                username = "${registryUsername}"
                password = "${registryPassword}"
            }
        }
        //镜像容器的配置信息
        container {
            environment = [SW_AGENT_NAME: "${project.name}".toString()]
            jvmFlags = ['-Duser.timezone=Asia/Shanghai', '-javaagent:/skywalking/agent/skywalking-agent.jar']
            creationTime = "USE_CURRENT_TIMESTAMP"
            volumes = ['/skywalking/agent']
        }
        //添加skyWalking agent探针
        extraDirectories {
            paths {
                path {
                    from = agentFile
                    into = '/skywalking/agent'
                }
            }
        }
    }
}




